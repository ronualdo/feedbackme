buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'feedbackme-service'
    version = '0.1.0'
}

sourceSets {
    acceptance {
        compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.output
    }
}

idea {
    module {
        testSourceDirs += file('src/acceptance/java')
        scopes.PROVIDED.plus += [ configurations.acceptanceCompile ]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.postgresql:postgresql:9.4-1206-jdbc42'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit'
    acceptanceCompile 'org.springframework.boot:spring-boot-starter-test'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task stage {
    dependsOn build
}

task acceptance(type: Test) {
    testClassesDir = sourceSets.acceptance.output.classesDir
    classpath = sourceSets.acceptance.runtimeClasspath + sourceSets.acceptance.compileClasspath
}
build.dependsOn acceptance

